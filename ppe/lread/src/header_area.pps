
PROCEDURE DrawHeaderArea()
    
    IF !MessageIndexBuilt THEN
        BuildMessageIndex()
    ENDIF

    IF LastFirstMsg != FirstMsg THEN
        LastFirstMsg = FirstMsg
        Redraw = TRUE
    ELSE 
        Redraw = FALSE
    ENDIF
;$IF RUNTIME < 400
    INTEGER Offset
    STRING MSGS_FILE = ConfInfo(CONF, 12)
    IF !Exist(MSGS_FILE) THEN
        Cls
        PRINT "MSGS MSGS_FILE not found: '" + MSGS_FILE + "'"
        WAIT
        STOP
    ENDIF

    FOPEN 2, MSGS_FILE + ".IDX", O_RD, S_DW
    IF FErr(2) THEN
        Cls
        PRINT "Error opening message index file : '", MSGS_FILE + ".IDX'"
        WAIT
        STOP
    ENDIF
    FOPEN 3, MSGS_FILE, O_RD, S_DW
;$ENDIF
    FOR I = 0 TO Lines
        INTEGER O = FirstMsg + I
        IF O >= ValidMessageCount THEN
            DefColor
            AnsiPos AreaX, AreaY + I
            ClrEOL
            CONTINUE
        ENDIF

        IF !Redraw && O != Cursor && O != OldCursor THEN
            CONTINUE
        ENDIF

        AnsiPos AreaX, AreaY + I
        IF O == Cursor THEN
            Print SELECTED_MSG_HDR_ATTRIBUTE
        ELSE
            Print MSG_HDR_ATTRIBUTE
        ENDIF 
        
        INTEGER MSG_NUM = GetActualMessageNumber(O)
        
        IF MSG_NUM == -1 THEN
            DefColor
            ClrEOL
            CONTINUE
        ENDIF
        

        IF FROM[O] == "" THEN 
;$IF RUNTIME < 400
            INTEGER IDX_POS = MSG_NUM - LOW
            FSeek 2, IDX_POS * 64, SEEK_SET
            FRead 2, Offset, 4
            FRead 2, J, 4
            FRead 2, LINE, 25
            TO[O] = Left(StripATX(LINE), 12) + " "
            FRead 2, LINE, 25
            FROM[O] = Left(StripATX(LINE), 12) + " "
            FRead 2, J, 1
            FRead 2, J, 2
            DATETXT[O] = STRING(TODATE(J))
 
            FSeek 3, Offset + 83, SEEK_SET
            FRead 3, LINE, 25
            SUBJ[O] = Left(StripATX(LINE), 36) + " "
;$ELSE
            FROM[O] = Left(StripATX(GetMsgHdr(CONF, MSG_NUM, HDR_FROM)), 12) + " "
            SUBJ[O] = Left(StripATX(GetMsgHdr(CONF, MSG_NUM, HDR_SUBJ)), 36) + " "
            TO[O] = Left(StripATX(GetMsgHdr(CONF, MSG_NUM, HDR_TO)), 12) + " "
            DATETXT[O] = STRING(GetMsgHdr(CONF, MSG_NUM, HDR_DATE))
;$ENDIF
        ENDIF
        Print Right(STRING(MSG_NUM), 4), " ", SUBJ[o], FROM[o], TO[o], DATETXT[O]
    NEXT
;$IF RUNTIME < 400
    FClose 2
    FClose 3
;$ENDIF
    OldCursor = Cursor
    DefColor
ENDPROC
